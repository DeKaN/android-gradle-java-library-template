buildscript {
    repositories {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "com.gradle:build-scan-plugin:1.3"
        classpath "com.android.tools.build:gradle:2.2.2"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.1"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.6.3"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.13.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.1"
    }
}

repositories {
    maven {
        url "https://plugins.gradle.org/m2/"
    }
}

group = "burrows.apps"
version = "1.0"

apply from: rootProject.file("gradle/buildScan.gradle")
apply plugin: "com.android.library"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.github.ben-manes.versions"
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    compileSdkVersion 25
    buildToolsVersion "25"

    defaultConfig {
        versionCode 1
        versionName "1.0"
        minSdkVersion 11
        targetSdkVersion 25
        consumerProguardFile "consumer-proguard-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
            proguardFile "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries !Boolean.valueOf(System.getenv("TRAVIS"))
    }

    lintOptions {
        textReport true
        textOutput "stdout"
        checkAllWarnings true
        warningsAsErrors true
        showAll true
        explainIssues true
        disable "UnusedResources",              // Unused will be removed on release
            "IconExpectedSize",             // Using the material icons provided from Google
            "GoogleAppIndexingApiWarning",  // We might want to index our app later
            "InvalidPackage",               // Butterknife, Okio and Realm
            "ResourceType",                 // Annotation binding
            "GradleDependency",
            "NewerVersionAvailable",
            "PropertyEscape"                // CI issue with sdk.dir in local.properties
    }

    testOptions {
        unitTests {
            all {
                jacoco {
                    includeNoLocationClasses = true
                }
            }
        }
    }
}

// https://github.com/tbroyer/gradle-errorprone-plugin/issues/18
configurations {
    all {
        resolutionStrategy {
            force "com.google.errorprone:error_prone_core:2.0.5"
        }
    }
}

dependencies {
    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:25.0.0"

    // Android Support Library for the CardView
    compile "com.android.support:cardview-v7:25.0.0"

    // Android Support Library for the RecyclerView
    compile "com.android.support:recyclerview-v7:25.0.0"

    // Google Play Services
    compile "com.google.android.gms:play-services-ads:9.4.0"
    compile "com.google.android.gms:play-services-basement:9.4.0"
    compile "com.google.android.gms:play-services-base:9.4.0"

    // Junit
    testCompile "junit:junit:4.12"

    // Mockito
    testCompile "org.mockito:mockito-core:1.10.19"

    // EasyMock
    testCompile "org.easymock:easymock:3.4"

    // PowerMock
    testCompile "org.powermock:powermock-core:1.6.5"
    testCompile "org.powermock:powermock-module-junit4:1.6.5"

    // PowerMock + EasyMock extension
    testCompile "org.powermock:powermock-api-easymock:1.6.5"

    // Robolectric + Support v4
    testCompile "org.robolectric:shadows-support-v4:3.1.2"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"

    // AssertJ
    testCompile "com.android.support:support-annotations:24.2.1"
    testCompile "com.squareup.assertj:assertj-android:1.1.1"
}
