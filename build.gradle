buildscript {
    ext.repos = {
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    repositories repos

    dependencies {
        classpath "com.android.tools.build:gradle:2.1.2"
    }
}

plugins {
    id "com.gradle.build-scan" version "1.0"
    id "net.ltgt.errorprone" version "0.0.8"
    id "com.github.kt3k.coveralls" version "2.6.3"
    id "com.github.ben-manes.versions" version "0.13.0"
    id "com.jfrog.bintray" version "1.7"
    id "maven"
    id "signing"
    id "maven-publish"
    id "build-dashboard"
    id "pmd"
    id "jacoco"
    id "findbugs"
    id "checkstyle"
}

repositories repos

group = "burrows.apps"
version = "1.0"

apply plugin: "com.android.library"
apply from: rootProject.file("gradle/buildScan.gradle")
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/android.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    defaultConfig {
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles(getDefaultProguardFile("proguard-android-optimize.txt"),
                    "proguard-rules.pro")
        }
    }
}

// https://github.com/tbroyer/gradle-errorprone-plugin/issues/18
configurations {
    all {
        resolutionStrategy {
            force "com.google.errorprone:error_prone_core:2.0.5"
        }
    }
}

dependencies {
    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:24.1.1"

    // Android Support Library for the CardView
    compile "com.android.support:cardview-v7:24.1.1"

    // Android Support Library for the RecyclerView
    compile "com.android.support:recyclerview-v7:24.1.1"

    // Google Play Services
    compile "com.google.android.gms:play-services-ads:9.2.1"
    compile "com.google.android.gms:play-services-basement:9.2.1"
    compile "com.google.android.gms:play-services-base:9.2.1"

    // Junit
    testCompile "junit:junit:4.12"

    // Mockito
    testCompile "org.mockito:mockito-core:2.0.87-beta"

    // EasyMock
    testCompile "org.easymock:easymock:3.4"

    // PowerMock
    testCompile "org.powermock:powermock-core:1.6.5"
    testCompile "org.powermock:powermock-module-junit4:1.6.5"

    // PowerMock + EasyMock extension
    testCompile "org.powermock:powermock-api-easymock:1.6.5"

    // Robolectric + Support v4
    testCompile "org.robolectric:shadows-support-v4:3.1.1"
    testCompile "org.khronos:opengl-api:gl1.1-android-2.1_r1"

    // AssertJ
    testCompile "com.android.support:support-annotations:24.1.0"
    testCompile "com.squareup.assertj:assertj-android:1.1.1"
}
