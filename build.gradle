buildscript {
    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "com.gradle:build-scan-plugin:1.4"
        classpath "com.android.tools.build:gradle:2.2.3"
        classpath "com.getkeepsafe.dexcount:dexcount-gradle-plugin:0.6.2"
        classpath "org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.1"
        classpath "com.github.ben-manes:gradle-versions-plugin:0.14.0"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
    }
}

repositories {
    maven { url "https://plugins.gradle.org/m2/" }
}

task wrapper(type: Wrapper) {
    gradleVersion = "3.4"
    distributionType = Wrapper.DistributionType.ALL
}

group = "burrows.apps"
version = "1.0"

apply from: rootProject.file("gradle/buildScan.gradle")
apply plugin: "com.android.library"
apply plugin: "com.getkeepsafe.dexcount"
apply plugin: "com.github.ben-manes.versions"
apply from: rootProject.file("gradle/compile.gradle")
apply from: rootProject.file("gradle/codeQuality.gradle")
apply from: rootProject.file("gradle/publishing.gradle")

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.2"

    defaultConfig {
        versionCode 1
        versionName "1.0"
        minSdkVersion 16
        targetSdkVersion 25
        consumerProguardFile "consumer-proguard-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFile getDefaultProguardFile("proguard-android-optimize.txt")
            proguardFile "proguard-rules.pro"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries !Boolean.valueOf(System.getenv("TRAVIS"))
    }

    lintOptions {
        textOutput "stdout"
        checkAllWarnings true
        warningsAsErrors true
        disable "UnusedResources"              // Unused will be removed on release
        disable "IconExpectedSize"             // Using the material icons provided from Google
        disable "GoogleAppIndexingApiWarning"  // We might want to index our app later
        disable "InvalidPackage"               // Butterknife, Okio and Realm
        disable "ResourceType"                 // Annotation binding
        disable "GradleDependency"
        disable "NewerVersionAvailable"
    }

    testOptions.unitTests {
        all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
}

// https://github.com/tbroyer/gradle-errorprone-plugin/issues/18
configurations.all {
    resolutionStrategy {
        force "com.google.errorprone:error_prone_core:2.0.5"
    }
}

dependencies {
    // Android Support Library for AppCompat
    compile "com.android.support:appcompat-v7:25.2.0"

    // Android Support Library for the CardView
    compile "com.android.support:cardview-v7:25.2.0"

    // Android Support Library for the RecyclerView
    compile "com.android.support:recyclerview-v7:25.2.0"

    // Google Play Services
    compile "com.google.android.gms:play-services-ads:10.2.0"
    compile "com.google.android.gms:play-services-basement:10.2.0"
    compile "com.google.android.gms:play-services-base:10.2.0"

    // Junit
    testCompile "junit:junit:4.12"

    // Mockito
    testCompile "org.mockito:mockito-core:2.7.12"

    // AssertJ
    testCompile "com.android.support:support-annotations:25.2.0"
    testCompile "org.assertj:assertj-core:1.7.1"
}
